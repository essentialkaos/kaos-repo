# Bibop recipe for Erlang 26
# See more: https://kaos.sh/bibop

pkg erlang26

var libressl_ver 3.8.4

var epmd_service epmd
var epmd_port    4369

command "-" "Check environment"
  app epmd
  app erl
  app erlc
  app escript

  service-present {epmd_service}

command "-" "Check zlib linking"
  !lib-linked {ERLANG_BIN_DIR}/beam.smp libz.so.1

command "erl -compile test" "Compile basic script"
  exit 0
  exist test.beam

command "erl -noshell -s test main -s init stop" "Execute basic script"
  exit 0
  output-contains "ERLANG WORKS"

command "escript ssl-info.erl" "Check version of SSL/TLS library"
  exit 0
  output-contains "LibreSSL {libressl_ver}"

command "systemctl start {epmd_service}" "Start Erlang Port Mapper Daemon"
  wait-service {epmd_service} 5

command "systemctl status {epmd_service}" "Check status of Erlang Port Mapper Daemon"
  expect "active (running)"
  connect tcp :{epmd_port}

command "systemctl stop {epmd_service}.socket" "Stop Erlang Port Mapper Daemon (with socket service)"
  !wait-service {epmd_service} 5
  !connect tcp :{epmd_port}
